apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

static def gitBranch() {
    def branch = 'GitHub'
    try {
        def gitcheck = 'command -v git >/dev/null 2>&1'.execute()
        gitcheck.waitFor()
        if (gitcheck.exitValue() == 0) {
            def proc = 'git rev-parse --abbrev-ref HEAD'.execute()
            proc.in.eachLine { line -> branch = line }
            proc.err.eachLine { line -> println line }
            proc.waitFor()
        }
    } catch (Exception e) {
        // Do nothing
        println e
    }
    branch
}

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.0 rc2'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28

        renderscriptTargetApi 28 //must match target sdk and build tools
        vectorDrawables.useSupportLibrary = true

        applicationId "code.name.monkey.retromusic"
        versionCode 319
        versionName '3.1.400'

        multiDexEnabled true

        buildConfigField("String", "GOOGLE_PLAY_LICENSING_KEY", "\"${getProperty(getProperties('../public.properties'), 'GOOGLE_PLAY_LICENSE_KEY')}\"")
    }
    signingConfigs {
        release {
            Properties properties = getProperties('/Users/hemanths/Desktop/KeepSafe/retro.properties')
            storeFile file(getProperty(properties, 'storeFile'))
            keyAlias getProperty(properties, 'keyAlias')
            storePassword getProperty(properties, 'storePassword')
            keyPassword getProperty(properties, 'keyPassword')
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "cast_app_id", "BA9C3F5E"
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG (' + gitBranch() + ')'
            resValue "string", "cast_app_id", "BA9C3F5E"

        }
    }


    flavorDimensions "default"

    productFlavors {
        normal {
            versionCode defaultConfig.versionCode + 10000
            versionName defaultConfig.versionName + "_" + getDate()
            dimension "default"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
    androidExtensions {
        experimental = true
    }

}

def getProperties(String fileName) {
    final Properties properties = new Properties()
    def file = file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

static def getProperty(Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing"
}

static def getDate() {
    new Date().format('MMdd')
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.fragment:fragment:1.1.0-alpha05'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha04'
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.palette:palette:1.0.0"
    implementation 'androidx.annotation:annotation:1.1.0-alpha02'
    implementation 'androidx.preference:preference:1.1.0-alpha04'
    implementation "androidx.legacy:legacy-support-v13:1.0.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation "com.afollestad.material-dialogs:core:$materialDialog"
    implementation "com.afollestad.material-dialogs:core:$materialDialog"
    implementation 'com.afollestad.material-dialogs:input:2.0.0'
    implementation 'com.afollestad.material-dialogs:color:2.0.0'
    implementation 'com.afollestad:material-cab:0.1.12'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.anjlab.android.iab.v3:library:1.0.44'
    /*UI Library*/
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'
    implementation 'com.r0adkll:slidableactivity:2.0.6'
    /*Backend all*/
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.0-kmod3'
    implementation 'com.github.AdrienPoupa:jaudiotagger:2.2.3'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:3.4.0.201406110918-r'
    implementation 'com.github.jetradarmobile:android-snowfall:1.2.0'
    implementation 'com.github.takahirom.downloadable.calligraphy:downloadable-calligraphy:0.1.3'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation project(':appthemehelper')
}

